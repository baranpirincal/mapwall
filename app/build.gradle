apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.vanniktech.android.apk.size'
apply plugin: "org.sonarqube"

def fabricKey = resolveProperty('fabricKey')
def googleKey = resolveProperty('googleKey')

if (fabricKey != null) {
    apply plugin: 'io.fabric'
}

def version = '0.1'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.devindi.wallpaper"
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fabricKey: "$fabricKey", googleKey: "$googleKey"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectVersion", version
        property "sonar.host.url", resolveProperty('sonarUrl')
        property "sonar.login", resolveProperty('sonarKey')
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.bluelinelabs:conductor:2.1.4'
    implementation 'com.bluelinelabs:conductor-archlifecycle:0.1.1'
    implementation 'org.osmdroid:osmdroid-android:6.0.1'
    implementation 'org.koin:koin-android-architecture:0.9.1'
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation 'com.google.android.gms:play-services-places:15.0.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
}

def resolveProperty(String key) {
    if (hasProperty(key)) return findProperty(key)
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty(key)
}
